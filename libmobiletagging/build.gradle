apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = (findProperty("group") ?: "com.github.kantarsifo")
version = (findProperty("version") ?: "LOCAL").toString()


android {
    namespace "se.kantarsifo.mobileanalytics.framework"
    compileSdk 34
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName version
        buildConfigField("long", "VERSION_CODE", "${defaultConfig.versionCode}")
        buildConfigField("String","VERSION_NAME","\"${defaultConfig.versionName}\"")
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }

    lintOptions {
        checkReleaseBuilds false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.2.4'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'androidx.activity:activity-ktx:1.9.3'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.8.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    api("com.google.androidbrowserhelper:androidbrowserhelper:2.5.0")

}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = project.group.toString()
                artifactId = "SifoInternetAndroidSDK"
                version = project.version.toString()
                from components.release
                pom {
                    name = "Sifo Internet Android SDK"
                    description = "Kantar Sifo Internet Android SDK"
                    url = "https://github.com/kantarsifo/SifoInternetAndroidSDK"
                }
            }
        }
    }
}


